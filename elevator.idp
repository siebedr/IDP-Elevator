// First name: Siebe
// Last name: Dreesen
// Student number: r0884600


// FIXED VOCABULARY
// DO NOT CHANGE THIS VOCABULARY
LTCvocabulary V_fixed {
    type Time isa nat			// A set of time points
    Start: Time 				// The initial time point
    partial Next(Time): Time 	// The successor time point of time point t

    type Floor isa nat									// The set of floors
    type DoorState constructed from {Closed, Open}		// A set of possible states of the elevator’s doors

    // Fluents
    ElPosition(Time):Floor				// The floor of the elevator at time point t
    ElDoorState(Time):DoorState			// The state of the elevator’s doors at time point t
    UnansweredRequest(Time, Floor)		// Denotes that there is an unanswered request at floor f at time point t

    // Actions
    MakeRequest(Time, Floor) 			// Denotes that a request is made for floor f at time point t
}


// STUDENT VOCABULARY
// Here you can add new types, functions and predicates
LTCvocabulary V_student {
    extern vocabulary V_fixed
    
    MinFloor: Floor
    MaxFloor: Floor
            
    type ElAction constructed from {Up, Down, Stay, OpenDoor, CloseDoor}
    do(Time, ElAction) 
    
    // Needed for the scanning method, direction can only change if nothing in this direction.
    //type Direction constructed from {GoingUp, GoingDown, Idle}
    //ElDirection(Time): Direction
    //C_ElDirection(Time, Direction)
    //Cn_ElDirection(Time, Direction)
    
    // Elevator position, no initial needed becuase starts at bottom = 0
    C_ElPosition(Time, Floor)
    Cn_ElPosition(Time, Floor)
    
    // Elevator door state, no initial needed becuase starts closed
    C_ElDoorState(Time, DoorState)
    Cn_ElDoorState(Time, DoorState)
    
    // Unanswered request
    C_UnansweredRequest(Time, Floor)
    Cn_UnansweredRequest(Time, Floor)
}

// TIME THEORY
// DO NOT CHANGE THIS THEORY
Theory T_fixed : V_student {
    // Defining time
    {
        Start = MIN[:Time].
        ! t: Next(t) = t+1 <- Time(t+1).
    }
}


// STUDENT THEORY
// Here you should model the dynamic elevator system
Theory T_student:V_student {
    {
        MinFloor = MIN[:Floor].
		MaxFloor = MAX[:Floor].
    }
    
    
    // CAUSE PREDICATES
    {
        ElPosition(Start) = 0.
        ! t[Time] f[Floor]: ElPosition(Next(t)) = f <- C_ElPosition(t, f).
        ! t[Time] f[Floor]: ElPosition(Next(t)) = f <- ElPosition(t) = f & ~Cn_ElPosition(t, f).
    }
    {
    	! t[Time] f[Floor]: C_ElPosition(t, f+1) <- do(t, Up) & ElPosition(t) = f.
        ! t[Time] f[Floor]: C_ElPosition(t, f-1) <- do(t, Down) & ElPosition(t) = f.
        ! t[Time] f[Floor]: C_ElPosition(t, f) <- do(t, Stay) & ElPosition(t) = f.
        ! t[Time] f[Floor]: Cn_ElPosition(t, f) <- ? fx[Floor]: C_ElPosition(t, fx) & f ~= fx.
    }
    
    {
        ! t[Time] f[Floor]: UnansweredRequest(Next(t), f) <- C_UnansweredRequest(t, f).
        ! t[Time] f[Floor]: UnansweredRequest(Next(t), f) <- UnansweredRequest(t, f) & ~Cn_UnansweredRequest(t, f).
    }
    {
        ! t[Time] f[Floor]: C_UnansweredRequest(t, f) <- MakeRequest(t, f) & ~UnansweredRequest(t, f).
        ! t[Time] f[Floor]: Cn_UnansweredRequest(t, f) <- ElPosition(t) = f & do(t, CloseDoor). // De lift is er al geweest en heeft zijn deuren geopend, maakt niet uit wanneer... moet enkel later gebeurd zijn
    }
    
    {
        //ElDirection(Start) = Idle.
        //! t[Time] d[Direction]: ElDirection(Next(t)) = d <- C_ElDirection(t, d).
        //! t[Time] d[Direction]: ElDirection(Next(t)) = d <- ElDirection(t) = d & ~Cn_ElDirection(t, d).
    }
    {
    	//! t[Time]: C_ElDirection(t, GoingUp) <- do(t, Up) | (ElDoorState(t) ~= Closed & ElDirection(t-1) = GoingUp) .
      	//! t[Time]: C_ElDirection(t, GoingDown) <- do(t, Down) | (ElDoorState(t) ~= Closed & ElDirection(t-1) = GoingDown).
        //! t[Time]: C_ElDirection(t, Idle) <- do(t, Stay).
        //! t[Time] d[Direction]: Cn_ElDirection(t, d) <- ? dx[Direction]: C_ElDirection(t, dx) & d ~= dx.
    }
    
    {
        ElDoorState(Start) = Closed.
        ! t[Time] ds[DoorState]: ElDoorState(Next(t)) = ds <- C_ElDoorState(t, ds).
        ! t[Time] ds[DoorState]: ElDoorState(Next(t)) = ds <- ElDoorState(t) = ds & ~Cn_ElDoorState(t, ds).
    }
    {
        ! t[Time]: C_ElDoorState(t, Open) <- do(t, OpenDoor) & ElDoorState(t) = Closed.
   		! t[Time]: C_ElDoorState(t, Closed) <- do(t, CloseDoor) & ElDoorState(t) = Open.
        ! t[Time] ds[DoorState]: Cn_ElDoorState(t, ds) <- ? dsx[DoorState]: C_ElDoorState(t, dsx) & ds ~= dsx.
    }
    
    
    // ACTIONS
    {
        // Basic movement
        // Go up if there is a higher unanswered request and this floor has no unanswered requests
        ! t[Time] f[Floor]: do(t, Up) <- ElPosition(t) = f & ~UnansweredRequest(t, f) & (? fx[Floor]: fx > f & UnansweredRequest(t, fx)).
        // Go down if there is a lower unanswered request and this floor has no unanswered requests
        ! t[Time] f[Floor]: do(t, Down) <- ElPosition(t) = f & ~UnansweredRequest(t, f) & (? fx[Floor]: fx < f & UnansweredRequest(t, fx)).
        // Stay still if their are no unanswered requests
        ! t[Time]: do(t, Stay) <- ~(?f[Floor]: UnansweredRequest(t, f)) & ElDoorState(t) = Closed.   
        
        // Door movement
        ! t[Time] f[Floor]: do(t, OpenDoor) <- ElPosition(t) = f & ElDoorState(t) = Closed & UnansweredRequest(t, f). // Deur is dicht en een unanswered request hier
   	 	! t[Time]: do(t, CloseDoor) <- ElDoorState(t) = Open. // Deur staat open en geen unanswered requests hier
    }
    
    
    // PRECONDITIONS
   	// 1 action at a time
	! t[Time]: ?=1 a[ElAction]: do(t, a).

	// Max en Min floor
    ! t[Time] f[Floor]: do(t, Up) & ElPosition(t) = f =>  f < MaxFloor.
    ! t[Time] f[Floor]: do(t, Down) & ElPosition(t) = f => f > MinFloor.
    
    // Doors closed when moving
    ! t[Time] : do(t, Up) | do(t, Down) => ElDoorState(t) = Closed.
    
    // Constraints on dos
    ! t[Time]: do(t, OpenDoor) => ElDoorState(t) = Closed.
    ! t[Time]: do(t, CloseDoor) => ElDoorState(t) = Open.
    
}


// MAIN PROCEDURE
// If you have to change the main procedure for debugging purposes, be careful and restore it back once you finish debugging.
procedure main() {
    local CompleteTheory = merge(T_fixed, T_student)

    //explainunsat(CompleteTheory, S1)
    
    // Change the structure name to try other structures (check the included file "structures.idp" for available provided structures, and feel free to add additional structures)
    model = onemodel(CompleteTheory, S4)

    // Printing model
    print(model)

    // Visualization
    initVisualization()
    visualizeElevator(model)
}

// File responsible for visualization
include "visualize_elevator.idp"

// Structures for testing
// You can open this file and check the given structures
include "structures.idp"
