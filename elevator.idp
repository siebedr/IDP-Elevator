// First name: Siebe
// Last name: Dreesen
// Student number: r0884600


// FIXED VOCABULARY
// DO NOT CHANGE THIS VOCABULARY
LTCvocabulary V_fixed {
    type Time isa nat			// A set of time points
    Start: Time 				// The initial time point
    partial Next(Time): Time 	// The successor time point of time point t

    type Floor isa nat									// The set of floors
    type DoorState constructed from {Closed, Open}		// A set of possible states of the elevator’s doors

    // Fluents
    ElPosition(Time):Floor				// The floor of the elevator at time point t
    ElDoorState(Time):DoorState			// The state of the elevator’s doors at time point t
    UnansweredRequest(Time, Floor)		// Denotes that there is an unanswered request at floor f at time point t

    // Actions
    MakeRequest(Time, Floor) 			// Denotes that a request is made for floor f at time point t
}


// STUDENT VOCABULARY
// Here you can add new types, functions and predicates
LTCvocabulary V_student {
    extern vocabulary V_fixed
    
    MinFloor: Floor
    MaxFloor: Floor
    
    // Requests in a specific direction 
    UpperRequest(Time, Floor)
    LowerRequest(Time, Floor)
    CurrentRequest(Time, Floor)
    
    // Checks if given floor is the closest on that moment
    IsClosestUpperRequest(Time, Floor)
    IsClosestLowerRequest(Time, Floor)
            
    type ElAction constructed from {Up, Down, Stay, OpenDoor, CloseDoor}
    do(Time, ElAction) 
    
    // Needed for the scanning method, direction can only change if no unanswered requests in this direction or at a max/min.
    type Direction constructed from {GoingUp, GoingDown, Idle}
    ElDirection(Time): Direction
    C_ElDirection(Time, Direction)
    Cn_ElDirection(Time, Direction)
    
    // Elevator position, no initial needed becuase starts at bottom = 0
    C_ElPosition(Time, Floor)
    Cn_ElPosition(Time, Floor)
    
    // Elevator door state, no initial needed becuase starts closed
    C_ElDoorState(Time, DoorState)
    Cn_ElDoorState(Time, DoorState)
    
    // Unanswered request
    C_UnansweredRequest(Time, Floor)
    Cn_UnansweredRequest(Time, Floor)
}

// TIME THEORY
// DO NOT CHANGE THIS THEORY
Theory T_fixed : V_student {
    // Defining time
    {
        Start = MIN[:Time].
        ! t: Next(t) = t+1 <- Time(t+1).
    }
}


// STUDENT THEORY
// Here you should model the dynamic elevator system
Theory T_student:V_student {
    {
        MinFloor = MIN[:Floor].
		MaxFloor = MAX[:Floor].
        
       	! t[Time] f[Floor] fx[Floor]: UpperRequest(t, fx) <- ElPosition(t) = f & fx > f & UnansweredRequest(t,fx).
        ! t[Time] f[Floor] fx[Floor]: LowerRequest(t, fx) <- ElPosition(t) = f & fx < f & UnansweredRequest(t,fx).
        ! t[Time] f[Floor] fx[Floor]: CurrentRequest(t, fx) <- ElPosition(t) = f & fx = f & UnansweredRequest(t,fx).

        // The closest floors above or under the current floor
        ! t[Time] f[Floor]: IsClosestUpperRequest(t, f) <- UpperRequest(t, f) & ~(?fx[Floor]: fx < f & UpperRequest(t,fx)).
        ! t[Time] f[Floor]: IsClosestLowerRequest(t, f) <- LowerRequest(t, f) & ~(?fx[Floor]: fx > f & LowerRequest(t,fx)).
    }
    
    
    // CAUSE PREDICATES
    // Elevator position
    {
        ElPosition(Start) = 0.
        ! t[Time] f[Floor]: ElPosition(Next(t)) = f <- C_ElPosition(t, f).
        ! t[Time] f[Floor]: ElPosition(Next(t)) = f <- ElPosition(t) = f & ~Cn_ElPosition(t, f).
    }
    {
        // How changes of the elevators position are caused and uncaused
    	! t[Time] f[Floor]: C_ElPosition(t, f+1) <- do(t, Up) & ElPosition(t) = f.
        ! t[Time] f[Floor]: C_ElPosition(t, f-1) <- do(t, Down) & ElPosition(t) = f.
        ! t[Time] f[Floor]: C_ElPosition(t, f) <- do(t, Stay) & ElPosition(t) = f.
        ! t[Time] f[Floor]: Cn_ElPosition(t, f) <- ? fx[Floor]: C_ElPosition(t, fx) & f ~= fx.
    }
    
    // Unanswered requests
    {
        ! t[Time] f[Floor]: UnansweredRequest(Next(t), f) <- C_UnansweredRequest(t, f).
        ! t[Time] f[Floor]: UnansweredRequest(Next(t), f) <- UnansweredRequest(t, f) & ~Cn_UnansweredRequest(t, f).
    }
    {
        // How changes of the elevators position are caused and uncaused
        ! t[Time] f[Floor]: C_UnansweredRequest(t, f) <- MakeRequest(t, f).
        // Elevator is on the specified floor with an unanswered request, has opened its doors and is about to close it's doors
        ! t[Time] f[Floor]: Cn_UnansweredRequest(t, f) <- ElPosition(t) = f & do(t, CloseDoor). 
    }
    
    // Direction, needed for the scanning methods. Changes when elevator is at a maximum are there are no unanswered requests in that direction
    {
        ElDirection(Start) = Idle.
        ! t[Time] d[Direction]: ElDirection(Next(t)) = d <- C_ElDirection(t, d).
        ! t[Time] d[Direction]: ElDirection(Next(t)) = d <- ElDirection(t) = d & ~Cn_ElDirection(t, d).
    }
    {		
        // Directions for up and down split in 2 cases: when going in the opposite direction and when idle
        ! t[Time] f[Floor]: C_ElDirection(t, GoingUp) <- ElDirection(t) = Idle & do(t, Up).
        ! t[Time] f[Floor]: C_ElDirection(t, GoingUp) <- ElDirection(t) = GoingDown & ElPosition(t) = f & (?fr[Floor]: UpperRequest(t, fr)) & ~(?fx[Floor]: UnansweredRequest(t, fx) & fx < f).

        ! t[Time] f[Floor]: C_ElDirection(t, GoingDown) <- ElDirection(t) = Idle & do(t, Down).
        ! t[Time] f[Floor]: C_ElDirection(t, GoingDown) <- ElDirection(t) = GoingUp & ElPosition(t) = f & (?fr[Floor]: LowerRequest(t, fr)) & ~(?fx[Floor]: UnansweredRequest(t, fx) & fx > f).
        
        ! t[Time] f[Floor]: C_ElDirection(t, Idle) <- ElPosition(t) = f & ~(?fx[Floor]: fx ~= f & UnansweredRequest(t, fx)).

        ! t[Time] d[Direction]: Cn_ElDirection(t, d) <- ? dx[Direction]: C_ElDirection(t, dx) & d ~= dx.
    }
    
    // Door state
    {
        ElDoorState(Start) = Closed.
        ! t[Time] ds[DoorState]: ElDoorState(Next(t)) = ds <- C_ElDoorState(t, ds).
        ! t[Time] ds[DoorState]: ElDoorState(Next(t)) = ds <- ElDoorState(t) = ds & ~Cn_ElDoorState(t, ds).
    }
    {
        // Behaviour of the doors
        ! t[Time]: C_ElDoorState(t, Open) <- do(t, OpenDoor) & ElDoorState(t) = Closed.
   		! t[Time]: C_ElDoorState(t, Closed) <- do(t, CloseDoor) & ElDoorState(t) = Open.
        ! t[Time] ds[DoorState]: Cn_ElDoorState(t, ds) <- ? dsx[DoorState]: C_ElDoorState(t, dsx) & ds ~= dsx.
    }
    
    
    // ACTIONS
    {
        // Basic movement
        // Go up if there is a higher unanswered request and this floor has no unanswered requests
        ! t[Time] f[Floor]: do(t, Up) <- ElPosition(t) = f & ~UnansweredRequest(t, f) & (? fx[Floor]: fx > f & UnansweredRequest(t, fx)) & ElDirection(t) = GoingUp.
        ! t[Time] f[Floor]: do(t, Up) <- ElDirection(t) = Idle & ElPosition(t) = f & (? fx[Floor]: IsClosestUpperRequest(t, fx) & ~(? fy[Floor]: IsClosestLowerRequest(t, fy) & f-fy < fx-f)).

        // Go down if there is a lower unanswered request and this floor has no unanswered requests
        ! t[Time] f[Floor]: do(t, Down) <- ElPosition(t) = f & ~UnansweredRequest(t, f) & (? fx[Floor]: fx < f & UnansweredRequest(t, fx)) & ElDirection(t) = GoingDown.
        ! t[Time] f[Floor]: do(t, Down) <- ElDirection(t) = Idle & ElPosition(t) = f & (? fx[Floor]: IsClosestLowerRequest(t, fx) & ~(? fy[Floor]: IsClosestUpperRequest(t, fy) & fy-f =< f-fx)).
        
        // Stay still if their are no unanswered requests
        ! t[Time]: do(t, Stay) <- ~(?f[Floor]: UnansweredRequest(t, f)) & ElDoorState(t) = Closed.   
        
        // Door movement
        // Open door if unanswered request on current floor
        ! t[Time] f[Floor]: do(t, OpenDoor) <- ElPosition(t) = f & ElDoorState(t) = Closed & UnansweredRequest(t, f).
        // Close door again if it is opened
   	 	! t[Time]: do(t, CloseDoor) <- ElDoorState(t) = Open.
    }
    
    
    // PRECONDITIONS
   	// 1 action at a time
	! t[Time]: ?=1 a[ElAction]: do(t, a).

	// Max en Min floor
    ! t[Time] f[Floor]: do(t, Up) & ElPosition(t) = f =>  f < MaxFloor.
    ! t[Time] f[Floor]: do(t, Down) & ElPosition(t) = f => f > MinFloor.
    
    // Doors closed when moving
    ! t[Time] : do(t, Up) | do(t, Down) => ElDoorState(t) = Closed.  
}


// MAIN PROCEDURE
// If you have to change the main procedure for debugging purposes, be careful and restore it back once you finish debugging.
procedure main() {
    local CompleteTheory = merge(T_fixed, T_student)

    //explainunsat(CompleteTheory, S3)
    
    // Change the structure name to try other structures (check the included file "structures.idp" for available provided structures, and feel free to add additional structures)
    model = onemodel(CompleteTheory, S6)

    // Printing model
    print(model)

    // Visualization
    initVisualization()
    visualizeElevator(model)
}

// File responsible for visualization
include "visualize_elevator.idp"

// Structures for testing
// You can open this file and check the given structures
include "structures.idp"
